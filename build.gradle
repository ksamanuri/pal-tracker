// Config specifically for build
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {

    ext {
        springBootVersion = "2.0.6.RELEASE"
        hibernateVersion = "6.0.13.Final"
        springWebVersion = "5.0.10.RELEASE"
        mockitoVersion = "2.23.4"
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

// Below this includes project dependencies

plugins {
    id 'java'
    id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'org.springframework.boot'

repositories {
    jcenter()
}

dependencies {
    compile("org.hibernate.validator:hibernate-validator:$hibernateVersion")
    compile("org.springframework:spring-web:$springWebVersion")
    compile("org.springframework:spring-webmvc:$springWebVersion")
    compile("org.springframework.boot:spring-boot-starter:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-json:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion")
    compile("mysql:mysql-connector-java:6.0.6")
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
    testCompile("org.mockito:mockito-core:$mockitoVersion")

}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
        "WELCOME_MESSAGE": "hello",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always",
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "WELCOME_MESSAGE": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always",
])


flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

springBoot {
    buildInfo()
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}